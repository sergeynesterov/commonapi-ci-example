cmake_minimum_required( VERSION 2.8.2 )

project( capicxx-download NONE )

include( ExternalProject )
ExternalProject_Add( capicxx-core-tools
   DOWNLOAD_COMMAND  wget "https://github.com/GENIVI/capicxx-core-tools/releases/download/${CAPI_VERSION}/commonapi-generator.zip"
   CONFIGURE_COMMAND ""
   BUILD_COMMAND     ""
   PREFIX            "${CAPI_DIR}"
   INSTALL_COMMAND   unzip -uo "${CAPI_DIR}/src/commonapi-generator.zip" -d "${CAPI_ROOT}/bin"
)

ExternalProject_Add( capicxx-dbus-tools
   DOWNLOAD_COMMAND  wget "https://github.com/GENIVI/capicxx-dbus-tools/releases/download/${CAPI_VERSION}/commonapi_dbus_generator.zip"
   CONFIGURE_COMMAND ""
   BUILD_COMMAND     ""
   PREFIX            "${CAPI_DIR}"
   INSTALL_COMMAND   unzip -uo "${CAPI_DIR}/src/commonapi_dbus_generator.zip" -d "${CAPI_ROOT}/bin"
)

ExternalProject_Add( capicxx-someip-tools
   DOWNLOAD_COMMAND  wget "https://github.com/GENIVI/capicxx-someip-tools/releases/download/${CAPI_VERSION}/commonapi_someip_generator.zip"
   CONFIGURE_COMMAND ""
   BUILD_COMMAND     ""
   PREFIX            "${CAPI_DIR}"
   INSTALL_COMMAND   unzip -uo "${CAPI_DIR}/src/commonapi_someip_generator.zip" -d "${CAPI_ROOT}/bin"
)

ExternalProject_Add( capicxx-core-runtime
   GIT_REPOSITORY    "https://github.com/GENIVI/capicxx-core-runtime.git"
   GIT_TAG           "${CAPI_VERSION}"
   PREFIX            "${CAPI_DIR}"
   BINARY_DIR        "${CAPI_DIR}/src/capicxx-core-runtime-build/${CMAKE_CXX_COMPILER_ID}"
   CMAKE_ARGS        "-DCMAKE_INSTALL_PREFIX=${CAPI_ROOT}"
                     "-DCMAKE_PROJECT_libcommonapi_INCLUDE=${CAPI_DIR}/FIX_WRONG_DLT_CONFIG.cmake"
)

ExternalProject_Add( vsomeip
   GIT_REPOSITORY    "https://github.com/GENIVI/vsomeip.git"
   GIT_TAG           "${VSOMEIP_VERSION}"
   PREFIX            "${CAPI_DIR}"
   BINARY_DIR        "${CAPI_DIR}/src/vsomeip-build/${CMAKE_CXX_COMPILER_ID}"
   CMAKE_ARGS        "-DCMAKE_INSTALL_PREFIX=${CAPI_ROOT}"
                     "-DCMAKE_PROJECT_vsomeip_INCLUDE=${CAPI_DIR}/FIX_WRONG_DLT_CONFIG.cmake"
                     -DENABLE_SIGNAL_HANDLING=1
                     -DDIAGNOSIS_ADDRESS=0x10
)

ExternalProject_Add( capicxx-someip-runtime
   DEPENDS           vsomeip
   GIT_REPOSITORY    "https://github.com/GENIVI/capicxx-someip-runtime.git"
   GIT_TAG           "${CAPI_VERSION}"
   PREFIX            "${CAPI_DIR}"
   BINARY_DIR        "${CAPI_DIR}/src/capicxx-someip-runtime-build/${CMAKE_CXX_COMPILER_ID}"
   CMAKE_ARGS        "-DCMAKE_INSTALL_PREFIX=${CAPI_ROOT}"
                     -DUSE_INSTALLED_COMMONAPI=OFF
)

ExternalProject_Add( capicxx-dbus-runtime-clone
   GIT_REPOSITORY    "https://github.com/GENIVI/capicxx-dbus-runtime.git"
   GIT_TAG           "${CAPI_VERSION}"
   PREFIX            "${CAPI_DIR}"
   CONFIGURE_COMMAND ""
   BUILD_COMMAND     ""
   INSTALL_COMMAND   ""
)

ExternalProject_Add( dbus
   DEPENDS           capicxx-dbus-runtime-clone
   URL               "http://dbus.freedesktop.org/releases/dbus/dbus-${DBUS_VERSION}.tar.gz"
   UPDATE_COMMAND    ""
   PREFIX            "${CAPI_DIR}"
   LIST_SEPARATOR    +
   PATCH_COMMAND     sh -c "for i in ${CAPI_DIR}/src/capicxx-dbus-runtime-clone/src/dbus-patches/*.patch + do patch -p1 -i $i + done"
   CONFIGURE_COMMAND "${CAPI_DIR}/src/dbus/configure" "--prefix=${CAPI_ROOT}"
   BINARY_DIR        "${CAPI_DIR}/src/dbus-build/${CMAKE_CXX_COMPILER_ID}"
)

ExternalProject_Add( capicxx-dbus-runtime
   DEPENDS           dbus capicxx-dbus-runtime-clone
   DOWNLOAD_COMMAND  ""
   PREFIX            "${CAPI_DIR}"
   SOURCE_DIR        "${CAPI_DIR}/src/capicxx-dbus-runtime-clone"
   BINARY_DIR        "${CAPI_DIR}/src/capicxx-dbus-runtime-build/${CMAKE_CXX_COMPILER_ID}"
   CMAKE_ARGS        "-DCMAKE_INSTALL_PREFIX=${CAPI_ROOT}"
                     -DUSE_INSTALLED_COMMONAPI=OFF
                     -DUSE_INSTALLED_DBUS=OFF
)
